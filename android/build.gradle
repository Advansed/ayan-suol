// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
        classpath 'com.google.gms:google-services:4.4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // Применить ко всем модулям
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                packagingOptions {
                    pickFirst '**/kotlin-stdlib-*.jar'
                    pickFirst 'kotlin/**'
                }
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'

            eachDependency { details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    if (details.requested.name in ['kotlin-stdlib-jdk7', 'kotlin-stdlib-jdk8']) {
                        details.useTarget 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
                    }
                }
            }
        }

        // Исключить проблемные зависимости
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
